plugins {
    id 'org.asciidoctor.jvm.gems' version '3.1.0'
    id 'org.asciidoctor.jvm.revealjs' version '3.1.0'
    id "org.asciidoctor.decktape" version "3.1.0"
    id 'org.ajoberstar.git-publish' version '3.0.0-rc.1'
    id "org.ajoberstar.grgit" version "4.0.1"
}

apply plugin: 'java'

version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    ruby {
        gems()
    }
}

dependencies {
    asciidoctorGems 'rubygems:rouge:3.13.0'
}

revealjs {
    version = '3.1.0'
}

asciidoctorRevealJs {
    revealjsOptions {
        controls = true
        progressBar = true
        pushToHistory = true
        overviewMode = true
        touchMode = true
        slideNumber = false
        theme = "white"
        highlightJsThemeLocation = "${projectDir}/src/docs/asciidoc/css/highlight-theme.css"
    }

    toggleBuiltinPlugin 'pdf', true
    toggleBuiltinPlugin 'notes', false

    asciidoctorj {
        requires 'rouge'
        attributes 'build-gradle': file('build.gradle'),
                'icons': 'font',
                'imagesdir': 'images',
                'revealjs_center': 'false',
                'revealjs_pdfmaxpagesperslide': '1',
                'revealjs_height': '720',
                'revealjs_margin': '0.1',
                'revealjs_fragmentinurl': 'true',
                'revealjs_slidenumber': 'c',
                'revealjs_minscale': '0.2',
                'revealjs_maxscale': '2'
    }

    baseDirFollowsSourceDir()
    sourceDir file("${projectDir}/src/docs/asciidoc")
    outputDir file("${projectDir}/build/slides")

    resources {
        from("${projectDir}/src/docs/asciidoc/images") {
            include '**'
            into 'images'
        }

        from("${projectDir}/src/docs/asciidoc/css") {
            include 'gradle.css'
            into 'reveal.js/css/theme'
        }
    }
}

gitPublish {
    branch = 'gh-pages'

    contents {
        from "$buildDir/slides"
    }

    commitMessage = 'Publishing a new page'
}